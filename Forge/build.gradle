architectury {
	platformSetupLoomIde()
	forge()
}

apply from: 'https://raw.githubusercontent.com/SizableShrimp/ForgeUpdatesRemapper/main/remapper.gradle'

archivesBaseName = "${mod_name}-forge-${minecraft_version}"

loom {
	accessWidenerPath = project(":Common").loom.accessWidenerPath
	forge {
		convertAccessWideners = true
		extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
	}
}

sourceSets.main.resources.srcDir 'src/generated/resources'

configurations {
	common
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentForge.extendsFrom common
}

dependencies {
	forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
	modImplementation "software.bernie.geckolib:geckolib-forge-1.19:${geckolib_forge_version}"

	common(project(path: ":Common", configuration: "namedElements")) { transitive = false }
}

processResources {
//	from project(":Common").sourceSets.main.resources
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId project.group
			artifactId project.archivesBaseName
			version project.version
			artifact jar
		}
	}
	repositories {
		maven {
			url "file://" + System.getenv("local_maven")
		}
	}
}
