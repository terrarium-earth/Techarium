archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

architectury {
	platformSetupLoomIde()
	fabric()
}

loom {
	accessWidenerPath = file('src/main/resources/techarium_fabric.accesswidener')
	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			setConfigName("Fabric Server")
			ideConfigGenerated(true)
			runDir("run")
		}
	}
}

configurations {
	common
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

dependencies {
	common(project(path: ":Common", configuration: "namedElements")) { transitive false }
	modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
	modImplementation "software.bernie.geckolib:geckolib-fabric-1.19:${geckolib_fabric_version}"
}

processResources {
	from project(":Common").sourceSets.main.resources
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

jar {
	from("LICENSE") {
		rename { "${it}_${mod_name}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId project.group
			artifactId project.archivesBaseName
			version project.version
			from components.java
		}
	}

	repositories {
		maven {
			url "file://" + System.getenv("local_maven")
		}
	}
}
